// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Pausable.sol";
import "@openzeppelin/contracts/access/AccessControl.sol";
import "./AvaComplianceRegistry.sol";

contract AvaStablecoin is ERC20, ERC20Burnable, ERC20Pausable, AccessControl {
    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");
    bytes32 public constant PAUSER_ROLE = keccak256("PAUSER_ROLE");

    AvaComplianceRegistry public registry;

    constructor(address _registry) ERC20("Ava Stablecoin", "AUSD") {
        require(_registry != address(0), "Invalid registry");
        registry = AvaComplianceRegistry(_registry);

        _grantRole(DEFAULT_ADMIN_ROLE, msg.sender);
        _grantRole(MINTER_ROLE, msg.sender);
        _grantRole(PAUSER_ROLE, msg.sender);
    }

    function pause() public onlyRole(PAUSER_ROLE) {
        _pause();
    }

    function unpause() public onlyRole(PAUSER_ROLE) {
        _unpause();
    }

    function mint(address to, uint256 amount) public onlyRole(MINTER_ROLE) {
        _mint(to, amount);
    }

    function _update(address from, address to, uint256 value) internal override(ERC20, ERC20Pausable) {
        if (from != address(0) && to != address(0)) {
            // Check compliance for transfers
            require(registry.getComplianceLevel(to) == AvaComplianceRegistry.ComplianceLevel.KYC_Passed || 
                    registry.getComplianceLevel(to) == AvaComplianceRegistry.ComplianceLevel.Regulator, 
                    "Recipient not KYC verified");
            
            require(registry.getComplianceLevel(from) != AvaComplianceRegistry.ComplianceLevel.Blacklisted, 
                    "Sender is blacklisted");
        }
        super._update(from, to, value);
    }
}
